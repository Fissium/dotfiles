# source external files
[[ -r ${ZDOTDIR:-$HOME}/.zaliases ]] && source ${ZDOTDIR:-$HOME}/.zaliases
[[ -r ${ZDOTDIR:-$HOME}/.zfunctions ]] && source ${ZDOTDIR:-$HOME}/.zfunctions

# tab autocompletions
{{- if eq .chezmoi.os "darwin" }}
if type brew &>/dev/null
then
  FPATH="$(brew --prefix)/share/zsh/site-functions:${FPATH}"
  autoload -Uz compinit
  zmodload zsh/complist
  _comp_options+=(globdots)
  compinit -d $XDG_CACHE_HOME/zsh/zcompdump-$ZSH_VERSION
fi
{{- end }}
{{- if eq .chezmoi.os "linux" }}
autoload -Uz compinit
zstyle ':completion:*' menu select
zmodload zsh/complist
_comp_options+=(globdots)
compinit -d $XDG_CONFIG_HOME/zsh/zcompdump-$ZSH_VERSION
{{- end }}
compdef kubecolor=kubectl

# zsh-history-substring-search
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down

# No bell: Shut up Zsh
unsetopt beep

# history
export HISTSIZE=20000
export SAVEHIST=50000
export HISTFILE="$HOME/.config/zsh/history"
export HIST_STAMPS="%d/%m/%y %T"
setopt hist_ignore_dups     # do not record an event that was just recorded again
setopt hist_ignore_all_dups # delete an old recorded event if a new event is a duplicate
setopt hist_ignore_space    # do not record an event starting with a space
setopt hist_save_no_dups    # do not write a duplicate event to the history file
setopt inc_append_history   # write to the history file immediately, not when the shell exits
setopt share_history        # share history between terminals

# change word selection to exclude slashes
autoload -U select-word-style
select-word-style bash

# fix vi plugin conflicting with fzf
export ZVM_INIT_MODE=sourcing

# zsh-history-substring-search
export HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_FOUND=""

##? Clone a plugin, identify its init file, source it, and add it to your fpath.
function plugin-load {
  local plugin repo commitsha plugdir initfile initfiles=()
  : ${ZPLUGINDIR:=${ZDOTDIR:-~/.config/zsh}/plugins}
  for plugin in $@; do
    repo="$plugin"
    clone_args=(-q --depth 1 --recursive --shallow-submodules)
    # Pin repo to a specific commit sha if provided
    if [[ "$plugin" == *'@'* ]]; then
      repo="${plugin%@*}"
      commitsha="${plugin#*@}"
      clone_args+=(--no-checkout)
    fi
    plugdir=$ZPLUGINDIR/${repo:t}
    initfile=$plugdir/${repo:t}.plugin.zsh
    if [[ ! -d $plugdir ]]; then
      echo "Cloning $repo..."
      git clone "${clone_args[@]}" https://github.com/$repo $plugdir
      if [[ -n "$commitsha" ]]; then
        git -C $plugdir fetch -q origin "$commitsha"
        git -C $plugdir checkout -q "$commitsha"
      fi
    fi
    if [[ ! -e $initfile ]]; then
      initfiles=($plugdir/*.{plugin.zsh,zsh-theme,zsh,sh}(N))
      (( $#initfiles )) || { echo >&2 "No init file found '$repo'." && continue }
      ln -sf $initfiles[1] $initfile
    fi
    fpath+=$plugdir
    (( $+functions[zsh-defer] )) && zsh-defer . $initfile || . $initfile
  done
}

# list of github repos of plugins
repos=(
	jeffreytse/zsh-vi-mode
	romkatv/zsh-defer
  Aloxaf/fzf-tab
	zdharma-continuum/fast-syntax-highlighting
  zsh-users/zsh-history-substring-search
	zsh-users/zsh-autosuggestions
)
plugin-load $repos

#completion-style
zstyle ':completion:*:git-checkout:*' sort false
zstyle ':completion:*' list-colors '${(s.:.)LS_COLORS}'
zstyle ':completion:*' menu no
zstyle ':fzf-tab:*' fzf-flags --height=~10
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'lsd $realpath'
zstyle ':completion:*:descriptions' format '[%d]'

# fast-syntax-highlighting
zle_highlight=('paste:none')

# starship
eval "$(starship init zsh)"

# fzf
source <(fzf --zsh)

export FZF_CTRL_R_OPTS="
  --preview 'echo {}' --preview-window up:5:hidden:wrap
  --bind 'ctrl-/:toggle-preview'"

# uv
eval "$(uv generate-shell-completion zsh)"

# direnv
eval "$(direnv hook zsh)"
